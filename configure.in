dnl Process this file with autoconf to produce a configure script.

dnl Autoconfiscation for Subversion

dnl General Setup -----------------------

dnl First line is *required*;  sanity-checks that the our src dir exists.
AC_INIT(subversion/include/svn_types.h)
AC_CONFIG_AUX_DIR(ac-helpers)

dnl Generate config.nice early (before the args are munged)
SVN_CONFIG_NICE(config.nice)

dnl Required to init all of the `automake' macros, and to run typical macros
AM_INIT_AUTOMAKE(subversion, 0.1, no-define)

dnl AC_CONFIG_HEADER causes `autoheader' to produce config.h.in for us.
dnl   (We can add custom defines by creating ./acconfig.h if we wish.)
dnl Because we're using automake, we must use AM_ instead of AC_.  AM_ creates
dnl    rules to regenerate the header file whenever config files change.
AM_CONFIG_HEADER(config.h)

dnl Todo: we're English-only now, but GNU Gettext eventually?
dnl Set of available languages.
dnl ALL_LINGUAS="de fr es ko nl no pl pt sl sv"

dnl APR defines a number of useful autoconf macros. grab them.
sinclude(apr/apr_common.m4)


dnl Sub-package configuration ---------------------

dnl Possibly reconfigure packages in subdirectories
AC_ARG_ENABLE(subdir-config,
  [  --disable-subdir-config  do not reconfigure packages in subdirectories ],
  [ ],
  enable_subdir_config=yes)

if test "$enable_subdir_config" = "yes"; then
  RUN_SUBDIR_CONFIG_NOW(apr)
  expat_absdir=`cd $srcdir/expat-lite ; pwd`
  RUN_SUBDIR_CONFIG_NOW(neon, --with-expat=$expat_absdir/libexpat.la)
fi


dnl Get libraries and thread flags from APR ---------------------

if test -f apr/APRVARS; then
  . apr/APRVARS
  CPPFLAGS="$CPPFLAGS $EXTRA_CPPFLAGS"
  CFLAGS="$CFLAGS $EXTRA_CFLAGS"
  LIBS="$LIBS $EXTRA_LIBS"
else
  AC_MSG_ERROR([Must configure APR before Subversion.])
fi

dnl Check for programs ---------------------

dnl Look for a C compiler
AC_PROG_CC

dnl See if 'ln -s' works
AC_PROG_LN_S

dnl `autoscan' suggested we check for ranlib... why?
dnl AC_PROG_RANLIB

dnl Check for libtool -- we'll definitely need it for all our shared libs!
echo "configuring libtool now"
AM_PROG_LIBTOOL

dnl find Apache
SVN_FIND_APACHE


dnl Check for libraries --------------------

dnl AC_CHECK_LIB() calls go here, if we ever need any

# Build the filesystem library only if we have an appropriate version of
# Berkeley DB.
SVN_LIB_BERKELEY_DB(3, 1, 14)
case "$svn_lib_berkeley_db" in
  yes )
    LIBSVN_FS="libsvn_fs"
  ;;
  no )
    LIBSVN_FS=""
  ;;
esac
AC_SUBST(LIBSVN_FS)


dnl Check for header files ----------------

dnl Standard C headers
AC_HEADER_STDC

dnl unistd.h is currently used by vdelta.c
dnl    -- so is sys/stat.h (check for this?)
AC_CHECK_HEADERS(unistd.h)


dnl Check for typedefs, structures, and compiler characteristics ----------

dnl if compiler doesn't understand `const', then define it empty
AC_C_CONST

dnl if non-existent, define size_t to be `unsigned'
AC_TYPE_SIZE_T


dnl Check for library functions ----------

dnl libsvn_string uses memcmp()
AC_FUNC_MEMCMP

dnl svn_error's default warning handler uses vfprintf()
AC_FUNC_VPRINTF

dnl vdelta.c is using strerror() right now 
AC_CHECK_FUNCS(strerror)


dnl Process some configuration options ----------

AC_ARG_WITH(debug,
[  --with-debug            Turn on debugging and compile time warnings],
[
  if test "$withval" != "no"; then
    enable_debugging=yes
  fi
])

AC_ARG_WITH(maintainer-mode,
[  --with-maintainer-mode  Turn on debugging and very strict compile-time
                          warnings],
[
  if test "$withval" != "no"; then
    enable_debugging=yes
    if test "$GCC" = "yes"; then
      dnl SVN_DEBUG enables specific features for developer builds
      dnl AP_DEBUG enables specific (Apache) features for developer builds
      CFLAGS="$CFLAGS -Wmissing-prototypes -Wstrict-prototypes -Wmissing-declarations -Wpointer-arith -DSVN_DEBUG -DAP_DEBUG";
    fi
  fi
])

if test "$enable_debugging" = "yes"; then
  if test "$GCC" = "yes"; then
    CFLAGS="$CFLAGS -g -Wall";
  else
    CFLAGS="$CFLAGS -g";
  fi

  ## At the moment, we don't want optimization, because we're
  ## debugging.
  changequote(,)
  CFLAGS="`echo $CFLAGS' ' | sed -e 's/-O[^ ]* //'`"
  changequote([,])
fi


dnl Pass some config data ----------------------------

AC_DEFINE_UNQUOTED(SVN_VERSION, "$VERSION", [Version number of Subversion])


dnl Final step:  create all Makefiles ----------------------------

AC_OUTPUT([Makefile                                           \
           doc/Makefile                                       \
           expat-lite/Makefile                                \
           subversion/Makefile                                \
           subversion/tests-common/Makefile                   \
           subversion/libsvn_subr/Makefile                    \
           subversion/libsvn_subr/tests/Makefile              \
           subversion/libsvn_delta/Makefile                   \
           subversion/libsvn_delta/tests/Makefile             \
           subversion/libsvn_wc/Makefile                      \
           subversion/libsvn_wc/tests/Makefile                \
           subversion/libsvn_client/Makefile                  \
           subversion/client/Makefile                         \
           subversion/client/tests/Makefile                   \
           subversion/libsvn_fs/Makefile                      \
           subversion/libsvn_fs/tests/Makefile                \
           subversion/libsvn_ra_dav/Makefile                  \
           subversion/libsvn_ra_dav/tests/Makefile            \
           subversion/mod_dav_svn/Makefile                    \
           subversion/mod_dav_svn/tests/Makefile              \
          ])


dnl  Print warning messages about what we did and didn't configure at the 
dnl  end, where people will actually see them.
case "$svn_cv_lib_berkeley_db" in
  "no" )
    echo 1>&2
    AC_MSG_WARN([we have configured for a client-only build


the Subversion filesystem library, part of
the server, requires Berkeley DB version 3.1.14 or newer, which you
don't seem to have installed.  We have created makefiles which will
build the Subversion client code only, and skip the server.
You can find latest version of Berkeley DB at http://www.sleepycat.com.])
  ;;
esac
