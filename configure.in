dnl Process this file with autoconf to produce a configure script.

dnl Autoconfiscation for Subversion

dnl General Setup -----------------------

dnl First line is *required*;  sanity-checks that the our src dir exists.
AC_INIT(subversion/include/svn_types.h)

dnl Required to initialize all of the `automake' macros
AM_INIT_AUTOMAKE(subversion, 0.1, no-define)

dnl AC_CONFIG_HEADER causes `autoheader' to produce config.h.in for us.
dnl   (We can add custom defines by creating ./acconfig.h if we wish.)
dnl Because we're using automake, we must use AM_ instead of AC_.  AM_ creates
dnl    rules to regenerate the header file whenever config files change.
AM_CONFIG_HEADER(config.h)

dnl Todo: we're English-only now, but GNU Gettext eventually?
dnl Set of available languages.
dnl ALL_LINGUAS="de fr es ko nl no pl pt sl sv"

dnl APR defines a number of useful autoconf macros. grab them.
sinclude(apr/apr_common.m4)

dnl Please run ./configure in the apr and neon subdirectories.
dnl (Add any other sub-projects, like expat-lite, to this list.)
dnl   TEMPORARILY COMMENTED OUT:  
dnl RUN_SUBDIR_CONFIG_NOW(apr)
dnl RUN_SUBDIR_CONFIG_NOW(neon, --with-expat=../expat-lite)

dnl SET_MAKE -> `MAKE=make', unless MAKE is already defined.
dnl (automake's docs said this macro is required if we set SUBDIRS
dnl    in the top-level `Makefile.am')
AC_PROG_MAKE_SET


dnl Check for programs ---------------------

dnl Look for a C compiler
AC_PROG_CC

dnl Add some useful GCC-specific flags.
case "$GCC" in
  yes )
    ## GCC can provide some helpful warnings.
    CFLAGS="$CFLAGS -Wall -Wmissing-prototypes -Wpointer-arith"
    ## At the moment, we don't want optimization, because we're
    ## debugging.
    changequote(,)
    CFLAGS="`echo $CFLAGS' ' | sed -e 's/-O[^ ]* //'`"
    changequote([,])
  ;;
esac

dnl Look for a BSD-compatible install program
dnl  (Or we can include install-sh at our top level)
AC_PROG_INSTALL

dnl See if 'ln -s' works
AC_PROG_LN_S

dnl `autoscan' suggested we check for ranlib... why?
dnl AC_PROG_RANLIB

dnl Check for libtool -- we'll definitely need it for all our shared libs!
AM_PROG_LIBTOOL



dnl Check for libraries --------------------

dnl AC_CHECK_LIB() calls go here, if we ever need any



dnl Check for header files ----------------

dnl Standard C headers
AC_HEADER_STDC

dnl unistd.h is currently used by vdelta.c
dnl    -- so is sys/stat.h (check for this?)
AC_CHECK_HEADERS(unistd.h)


dnl Check for typedefs, structures, and compiler characteristics ----------

dnl if compiler doesn't understand `const', then define it empty
AC_C_CONST

dnl if non-existent, define size_t to be `unsigned'
AC_TYPE_SIZE_T


dnl Check for library functions ----------

dnl libsvn_string uses memcmp()
AC_FUNC_MEMCMP

dnl svn_error's default warning handler uses vfprintf()
AC_FUNC_VPRINTF

dnl vdelta.c is using strerror() right now 
AC_CHECK_FUNCS(strerror)


dnl Final step:  create all Makefiles ----------------------------

AC_OUTPUT([Makefile                                           \
           doc/Makefile                                       \
           expat-lite/Makefile                                \
           subversion/Makefile                                \
           subversion/tests-common/Makefile                   \
           subversion/libsvn_string/Makefile                  \
           subversion/libsvn_string/tests/Makefile            \
           subversion/libsvn_subr/Makefile                    \
           subversion/libsvn_subr/tests/Makefile              \
           subversion/libsvn_delta/Makefile                   \
           subversion/libsvn_delta/tests/Makefile             \
           subversion/libsvn_wc/Makefile                      \
           subversion/libsvn_wc/tests/Makefile                \
           subversion/libsvn_fs/Makefile                      \
          ])
